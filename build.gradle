plugins {
    id 'war'
}

group 'com.mika'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    wsImport
}

dependencies {
    compileOnly 'javax:javaee-api:8.0'
    compileOnly 'javax.xml.ws:jaxws-api:2.3.1'
    compileOnly 'com.sun.xml.ws:jaxws-ri:2.3.2-1'
    // https://stackoverflow.com/questions/48204141/replacements-for-deprecated-jpms-modules-with-java-ee-apis/48204154
    testImplementation 'jakarta.xml.ws:jakarta.xml.ws-api:2.3.3'
    // https://stackoverflow.com/questions/47199846/unable-to-create-saaj-meta-factory-after-packaging-as-jar
    testImplementation 'com.sun.xml.messaging.saaj:saaj-impl:1.5.1'
    testImplementation 'io.rest-assured:rest-assured:4.3.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'

    wsImport 'com.sun.xml.ws:jaxws-tools:2.3.3'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

war {
    archiveName 'fun-with-soap.war'
}

task wsImport(type: JavaExec) {
    description 'Generates Java classes from WSDL'
    classpath configurations.wsImport.files
    main = 'com.sun.tools.ws.WsImport'
    args '-target', '2.1',
        '-s', 'src/main/java',
        '-keep',
        '-Xnocompile',
        '-extension',
        '-encoding', 'UTF-8',
        'src/main/resources/calculator.wsdl'
}
